# All German (Q188) verbs and the currently implemented verb tenses for each.
# Enter this query at https://query.wikidata.org/.

# Not SELECT DISTINCT as we want to get verbs with both sein and haben as auxiliaries
SELECT
  ?infinitive ?pastParticiple ?auxiliaryVerb
  ?indPresFPS ?indPresSPS ?indPresTPS
  ?indPresFPP ?indPresSPP ?indPresTPP
  ?indPretFPS ?indPretSPS ?indPretTPS
  ?indPretFPP ?indPretSPP ?indPretTPP WHERE {

  ?lexeme a ontolex:LexicalEntry ;
    dct:language wd:Q188 ;
    wikibase:lexicalCategory wd:Q24905 .

  # Infinitive (required), past participle and the auxiliary verb(s)
  ?lexeme ontolex:lexicalForm ?infinitiveForm .
  ?infinitiveForm ontolex:representation ?infinitive ;
    wikibase:grammaticalFeature wd:Q179230 .

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pastParticipleForm .
    ?pastParticipleForm ontolex:representation ?pastParticiple ;
      wikibase:grammaticalFeature wd:Q12717679 ;
  } .

  # WIP: select select only auxiliary or both sein and haben and convert to labels
  OPTIONAL {
    ?lexeme wdt:P5401 ?auxiliaryVerbFrom ;
  } .

  # Indicative Present
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPresFPSForm .
    ?indPresFPSForm ontolex:representation ?indPresFPS ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPresSPSForm .
    ?indPresSPSForm ontolex:representation ?indPresSPS ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPresTPSForm .
    ?indPresTPSForm ontolex:representation ?indPresTPS ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPresFPPForm .
    ?indPresFPPForm ontolex:representation ?indPresFPP ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPresSPPForm .
    ?indPresSPPForm ontolex:representation ?indPresSPP ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPresTPPForm .
    ?indPresTPPForm ontolex:representation ?indPresTPP ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q192613 ;
  } .

  # Indicative Preterite
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPretFPSForm .
    ?indPretFPSForm ontolex:representation ?indPretFPS ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPretSPSForm .
    ?indPretSPSForm ontolex:representation ?indPretSPS ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPretTPSForm .
    ?indPretTPSForm ontolex:representation ?indPretTPS ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q110786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPretFPPForm .
    ?indPretFPPForm ontolex:representation ?indPretFPP ;
      wikibase:grammaticalFeature wd:Q21714344 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPretSPPForm .
    ?indPretSPPForm ontolex:representation ?indPretSPP ;
      wikibase:grammaticalFeature wd:Q51929049 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?indPretTPPForm .
    ?indPretTPPForm ontolex:representation ?indPretTPP ;
      wikibase:grammaticalFeature wd:Q51929074 ;
      wikibase:grammaticalFeature wd:Q146786 ;
      wikibase:grammaticalFeature wd:Q682111 ;
      wikibase:grammaticalFeature wd:Q442485 ;
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?auxiliaryVerbFrom rdfs:label ?auxiliaryVerb .
  }
}
