# All Swedish (Q9027) verbs and the currently implemented verb tenses for each.
# Enter this query at https://query.wikidata.org/.

SELECT ?activeVoiceInfinitive ?activeVoicePresent ?activeVoicePreterite
       ?activeVoicePluralPreterite ?activeVoiceSupine
       ?passiveVoiceInfinitive ?passiveVoicePresent ?passiveVoicePreterite
       ?passiveVoicePluralPreterite ?passiveVoiceSupine
       ?imperative WHERE {
  ?lexeme a ontolex:LexicalEntry ;
            dct:language wd:Q9027 ;
            wikibase:lexicalCategory wd:Q24905 .

  # Active Voice Infinitive (required)
  ?lexeme ontolex:lexicalForm ?activeVoiceInfinitiveForm .
  ?activeVoiceInfinitiveForm ontolex:representation ?activeVoiceInfinitive ;
                  wikibase:grammaticalFeature wd:Q1317831 ;
                  wikibase:grammaticalFeature wd:Q179230 ;

  # Active Voice Present
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?activeVoicePresentForm .
      ?activeVoicePresentForm ontolex:representation ?activeVoicePresent ;
                                wikibase:grammaticalFeature wd:Q1317831 ;
                                wikibase:grammaticalFeature wd:Q192613 ;
  } .

  # Active Voice Preterite
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?activeVoicePreteriteForm .
      ?activeVoicePreteriteForm ontolex:representation ?activeVoicePreterite ;
                                wikibase:grammaticalFeature wd:Q1317831 ;
                                wikibase:grammaticalFeature wd:Q442485 ;
  } .

  # Active Voice Plural Preterite
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?activeVoicePluralPreteriteForm .
      ?activeVoicePluralPreteriteForm ontolex:representation ?activeVoicePluralPreterite ;
                                wikibase:grammaticalFeature wd:Q1317831 ;
                                wikibase:grammaticalFeature wd:Q146786 ;
                                wikibase:grammaticalFeature wd:Q442485 ;
  } .

  # Active Voice Supine
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?activeVoiceSupineForm .
      ?activeVoiceSupineForm ontolex:representation ?activeVoiceSupine ;
                                wikibase:grammaticalFeature wd:Q1317831 ;
                                wikibase:grammaticalFeature wd:Q548470 ;
  } .

  # Imperative
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?imperativeForm .
      ?imperativeForm ontolex:representation ?imperative ;
                                wikibase:grammaticalFeature wd:Q22716 ;
  } .

  # Passive Voice Infivitive
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?passiveVoiceInfinitiveForm .
      ?passiveVoiceInfinitiveForm ontolex:representation ?passiveVoiceInfinitive ;
                                wikibase:grammaticalFeature wd:Q1194697 ;
                                wikibase:grammaticalFeature wd:Q179230 ;
  } .

  # Passive Voice Present
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?passiveVoicePresentForm .
      ?passiveVoicePresentForm ontolex:representation ?passiveVoicePresent ;
                                wikibase:grammaticalFeature wd:Q1194697 ;
                                wikibase:grammaticalFeature wd:Q192613 ;
  } .

  # Passive Voice Preterite
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?passiveVoicePreteriteForm .
      ?passiveVoicePreteriteForm ontolex:representation ?passiveVoicePreterite ;
                                wikibase:grammaticalFeature wd:Q1194697 ;
                                wikibase:grammaticalFeature wd:Q442485 ;
  } .

  # Passive Voice Plural Preterite
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?passiveVoicePluralPreteriteForm .
      ?passiveVoicePluralPreteriteForm ontolex:representation ?passiveVoicePluralPreterite ;
                                wikibase:grammaticalFeature wd:Q1194697 ;
                                wikibase:grammaticalFeature wd:Q146786 ;
                                wikibase:grammaticalFeature wd:Q442485 ;
  } .

  # Passive Voice Supine
  OPTIONAL {
      ?lexeme ontolex:lexicalForm ?passiveVoiceSupineForm .
      ?passiveVoiceSupineForm ontolex:representation ?passiveVoiceSupine ;
                                wikibase:grammaticalFeature wd:Q1194697 ;
                                wikibase:grammaticalFeature wd:Q548470 ;
  } .
}
