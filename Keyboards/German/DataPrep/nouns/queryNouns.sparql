# All German nouns, their gender, and their plural.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT ?singular ?plural ?gender WHERE {

  # All nouns or pronouns of a given language.
  VALUES ?nounTypes { wd:Q1084 wd:Q147276 }
  ?lexeme a ontolex:LexicalEntry ;
            wikibase:lexicalCategory ?noun ;
            dct:language ?language .
  FILTER(?noun = ?nounTypes )

  # Language is German.
  ?language wdt:P218 'de' .

  # Optional selection of nominative singular forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?singularForm .
    ?singularForm ontolex:representation ?singular ;
                  wikibase:grammaticalFeature wd:Q110786 ;
                  wikibase:grammaticalFeature wd:Q131105
  } .

  # Optional selection of nominative plural forms.
  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
                wikibase:grammaticalFeature wd:Q146786 ;
                wikibase:grammaticalFeature wd:Q131105
  } .

  # Optional selection of genders.
  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  } .

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE]".
    ?nounGender rdfs:label ?gender .
  }
}
